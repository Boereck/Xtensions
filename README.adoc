= Xtensions Library
:toc:
:toclevels: 4


== Introduction

This library is mainly a collection of https://www.eclipse.org/xtend/[Xtend] extension methods
for classes in the Java 8 standard library and the Xtend standard library. A few goals are:

* Adding extension methods to Java 8 JDK types to make them work more naturally with Xtend
* Making a select few Java 9 methods available on Java 8
* Providing support for iteration over primitive values without resort to boxing
* Adding some useful additional methods to existing standard JDK and Xtend standard lib classes


== Usage

Currently there is no p2 update site or maven repository available. To add this library to your project
you have to compile the library yourself. Please have a look at the <<Build>> section.


== Provided Functionality

This chapter will provide a high level overview on how to use the different parts of this library.
Unfortunately there is currently no built available via a maven or p2 repository. So the library
has to be built from source. See chapter <<Build>>.

When using the library in OSGi it is recommended to use package imports since the library may evolve
and split into multiple bundles in future releases.

The following chapters will give an overview over the most important extensions and types provided 
by the library. It does not include all methods. Please have a look at the sources or the JavaDocs
to explore all available functionality.

Most of the given examples in the following sections and some more are defined in the following 
test class:
link:tests/de.fhg.fokus.xtensions.tests/src/de/fhg/fokus/xtensions/Showcase.xtend[Showcase.xtend]


=== Extensions to Optional

The static factory Methods for creating `Optional` instances are not really meant to be used as 
statically imported methods. They have no meaningful names to be used this way. They also differ from
the commonly used names `some`, `none` and `maybe` which are used in many other languages.  
The class `OptionalIntExtensions` provides static factory methods with these common names
which are implemented to be inlined to the factory methods used by the JDK.

Examples:

	import static extension de.fhg.fokus.xtensions.optional.OptionalIntExtensions.*
	// ...
	val Optional<String> no = none
	val Optional<String> yes = some("yesss!")
	val Optional<String> dunno = maybe(possiblyNull())
	// ...
	private def String possiblyNull() {
		if(System.currentTimeMillis % 2 == 0) {
			"I'm in ur optional"
		} else {
			null
		}
	}

The optional class does not provide a `filter` method, that filters the optional based on the class
the wrapped object is instance of, as known e.g. from Xtend's filter methods on `Iterable`. 
The `OptionalIntExtensions` adds such a method, providing an instance check of the wrapped value.

Example:

	import static extension de.fhg.fokus.xtensions.optional.OptionalIntExtensions.*
	// ...
	val Optional<Object> optObj = some("Hi there!")
	val Optional<String> optStr = optObj.filter(String)
	optStr.ifPresent [
		println(it.toUpperCase)
	]

To bridge between APIs providing an `Optional` value and ones that expect
multiple values, the extension methods `asIterable`, `toList` and `toSet`
are provided to create immutable implementations of common JVM collection APIs.

 
The `Optional` class has a `map` method that can map the value present in the optional
to a value of another type. Unfortunately there is no method to map to a primitive type
returning a primitive optional, such as `OptionalInt`. The extension methods `mapInt`,
`mapLong`, and `mapDouble` allow mapping to primitive options without having to
box the resulting value.

Example:

	import static extension de.fhg.fokus.xtensions.optional.OptionalIntExtensions.*
	// ...
	val Optional<String> yes = some("yesss!")
	val OptionalInt lenOpt = yes.mapInt[length]
	val len = lenOpt.orElse(0)
	println("Length is " + len)


Some methods on Optional introduced in Java 9 are available as retrofitted extension methods.
When compiling a class using the extension method targeting Java 9, the native Optional method has precedence and will be used.
No changes in the source code has to be done to switch to the native Java 9 implementation.
The following instance methods of Optional are backported for Java 8:

* http://docs.oracle.com/javase/9/docs/api/java/util/Optional.html#or-java.util.function.Supplier-[Optional<T> or​(Supplier<? extends Optional<? extends T>> supplier)]
* http://docs.oracle.com/javase/9/docs/api/java/util/Optional.html#ifPresentOrElse-java.util.function.Consumer-java.lang.Runnable-[void ifPresentOrElse​(Consumer<? super T> action, Runnable emptyAction)]
* http://docs.oracle.com/javase/9/docs/api/java/util/Optional.html#stream--[Stream<T> stream​()]
​

As a shortcut for the `or` extension method, the `||` operator is provided. The `?:` operator is a shortcut for the `orElse` method on Optional.


=== Extensions to Primitive Optionals

Extensions to the primitive versions of Optional are provided by the following classes:

	de.fhg.fokus.xtensions.optional.OptionalIntExtensions
	de.fhg.fokus.xtensions.optional.OptionalLongExtensions
	de.fhg.fokus.xtensions.optional.OptionalDoubleExtensions

Same as for Optional, there is a `some` alias for the `OptionalInt.of`, `OptionalLong.of`, and `OptionalDouble.of`
methods (see <<Extensions to Optional>>).  
The methods `noInt`, `noLong`, and `noDouble` provide empty primitive Optionals.

The Open JDK / Oracle JDK currently does not cache OptionalInt and OptionalLong instances in the static factory method 
`OptionalInt.of(int)` and `OptionalLong.of(long)` as it is currently done for Integer creation in 
`Integer.valueOf(int)`. To provide such a caching static factory methods, the 
`OptionalIntExtensions.someOf(int)` and `OptionalLongExtensions.someOf(long)` method were 
introduced.

Example:

	if(someOf(42) === someOf(42)) {
		println("someOf caches instances")
	}

Stunningly, the primitive versions of Optional do not provide `map` and `filter` methods. These 
are provided as extension methods by this library.


=== Extensions to IntegerRange

IntegerRange is a handy type from the Xtend standard library which can
be constructed using the `..` operator. But the only way to iterate 
over the elements of the range is by boxing the integers while iterating.

The extensions provided by this library allow iterating over the primitive
values of the range.

One way to iterate over the range is to use Java 8 streams, by using the 
`stream` or `parallelStream` extension method from the class 
`de.fhg.fokus.xtensions.range.RangeExtensions`.
  
Exmaple: 

	import static extension de.fhg.fokus.xtensions.range.RangeExtensions.*
	// ...
	val range = (0..20).withStep(2)
	range.stream.filter[it % 5 == 0].sum

Another way to iterate over the elements of a range is to use the `forEachInt` method.

Example:

	import static extension de.fhg.fokus.xtensions.range.RangeExtensions.*
	// ...
	val range = (0..20).withStep(2)
	range.forEachInt [
		println(it)
	]

To interact with consumers expecting an `IntIterable` (see <<Primitive Iterables>>), which is a generic interface 
for iteration over primitive int values provided by this library, the extension method
`asIntIterable` was provided.


=== Extensions to Pair

The class `de.fhg.fokus.xtensions.pair.PairExtensions` provides extension methods for the type 
`org.eclipse.xtext.xbase.lib.Pair`.

The with-operator `=>` can be used to destructure a Pair into `key` and `value`.

Example:

	import static extension de.fhg.fokus.xtensions.pair.PairExtensions.*
	// ...
	val pair = "Foo" -> 3
	pair => [k,v|
		println(k + ' -> ' + v)
	]


The `combine` extension method takes a function to which key and value of a Pair is passed to,
to merge both objects. The result returned by the function will be returned by the `combine` method.
The difference to the `>>>` operator, provided by the <<Extensions to Functions,`FunctionExtensions`>>
is only that due to operator precedence calling further methods on the result needs further braces.

Example:

	import static extension de.fhg.fokus.xtensions.pair.PairExtensions.*
	import static extension org.eclipse.xtext.xbase.lib.InputOutput.*
	// ...
	val pair = "Foo" -> 3
	pair.combine[k,v| k + ' -> ' + v].println

////
TODO: Describe safeCombine   
////


=== Extensions to Primitive Arrays

The class `de.fhg.fokus.xtensions.iteration.PrimitiveArrayExtensions` contains extension methods for 
arrays of primitive values (int, long, double) to iterate with a forEach method consuming primitive values.

Example:

	val int[] arr = #[3,4,6]
	arr.forEachInt [
		println(it)
	]

Additionally the class allows to create primitive iterable wrapper objects (see <<Primitive Iterables>>).

TIP: Note that the JDK class http://docs.oracle.com/javase/8/docs/api/java/util/Arrays.html[`java.util.Arrays`] already contains 
static `stream` methods that can be used as extension methods to create Java 8 streams from primitive arrays.


=== Extensions to Streams

The class `de.fhg.fokus.xtensions.stream.StreamExtensions`

Java 8 streams are missing a few methods known from the Xtend iterable extension methods.
The one method that is probably most often used is the method to filter by type. This can easily
be retrofitted on the Streams API by an extension method. This extension method is provided
in the `StreamExtensions` class.

Example: 


	import static extension de.fhg.fokus.xtensions.stream.StreamExtensions.*
	// ...
	val s = Stream.of(42, "Hello", Double.NaN, "World")
		.filter(String)
		.collect(Collectors.joining(" "))

Note: Since joining Strings is a common operation, the `StringStreamExtensions` allow to call `join`
directly on the Stream. Have a look at <<Extensions to Streams of Strings>>.

Some other collectors, especially the ones bridging to the collections API are also used very often,
but using the collect method with the methods from the `Collectors` class is a bit verbose.  
As a shortcut the `StreamExtensions` class provides `toList`, `toSet`, and `toCollection` 
extension methods to the `Stream` class.

Example:

	import static extension de.fhg.fokus.xtensions.stream.StreamExtensions.*
	// ...
	val list = Stream.of("Foo", "Hello" , "Boo", "World")
		.filter[!contains("oo")]
		.map[toUpperCase]
		.toList

As a shortcut for the http://docs.oracle.com/javase/8/docs/api/java/util/stream/Stream.html#concat-java.util.stream.Stream-java.util.stream.Stream-[concat] method the `StreamExtensions` class provides a `+` operator.


Sometimes it is interesting to produce the cartesian product of two containers of elements. To produce all combinations of the elements of a stream with the elements of an `Iterable` (or a different source of a stream) this library provides the `combinations` extension methods. If no merging function is provided, the `combinations` extension methods will create a `org.eclipse.xtext.xbase.lib.Pair` object for each combination. If a merging function is provided, the resulting stream will hold the result of the merge of each combination.

Example:

	Stream.of("foo", "bar")
		.combinations(#["fun", "boo", "faz"])[a,b|a+b]
		.forEach[
			println(it)
		]


Java 9 provides a static factory methods for an infinite stream http://docs.oracle.com/javase/9/docs/api/java/util/stream/Stream.html#Iterate-java.lang.Object-java.util.function.UnaryOperator-[Stream.iterate(T,UnaryOperator<T>)]. A function with the same functionality is provided via `StreamExtensions`. There is even
an overloaded version of the static method that can be written as if the method would exist in the Stream class:

	Stream.iterate("na ")[it + it]
		.filter[length > 15]
		.findFirst
		.ifPresent [
			println(it + "Batman!")
		]

This method can be handy traversing a nested data structure of same-kind elements (e.g. moving up a containment hierarchy).

////
TODO: Describe combinations extension methods  
////


=== Extensions to Streams of Strings

Since Xtend can provide extension methods specifically for specializations of generic types,
it is possible to provide methods only available for `java.util.stream.Stream<String>`.
The class `de.fhg.fokus.xtensions.stream.StringStreamExtensions` provides such extension methods.

////
TODO: Describe join collector extension method  
TODO: Describe matching filter extension method  
TODO: Describe flatSplit mapping extension method  
TODO: Describe flatMatches mapping extension method  
TODO: Describe join collector
////


=== Extensions to Iterable

The `de.fhg.fokus.xtensions.iteration.IterableExtensions` class provides extension methods to `java.lang.Iterable`

Unfortunately the `java.lang.Iterable` interface does not provide a (default)
method for creating a `java.lang.Stream`.  It does provide a method to obtain a 
`Spliterator` which can be used to create a stream, but this is rather unpleasant to use.  
The `IterableExtensions` class provides the `stream` extension method to easily create
a stream from an iterable. This method will first check if the given iterable is instance of
`java.util.Collection`, since this class does provide a default `stream` method,
otherwise it will construct a stream from the spliterator provided by the iterable.

Analogous to the `stream` method the `IterableExtensions` class also provides a `parallelStream` method.

It is also possible to map an iterable to a primitive iterable (see <<from-iterables,Primitve Iterables / From Iterables>>).


////
TODO: Describe extension method `collect`  
////


=== Extensions to PrimitiveIterators

The primitive iterators defined in the JDK as sub-interfaces of `java.util.PrimitiveIterator` 
do not provide combinators like the ones provided by Xtend. These combinators, however, do take some 
efforts to implement. Instread, this library provides the class `de.fhg.fokus.xtensions.iteration.PrimitiveIteratorExtensions` provides methods to 
create primitive streams (from `java.util.stream`) for the remaining elements of a given iterator via the 
extension methods `streamRemaining` or `parallelStreamRemaining`.


=== Primitive Iterables

The JDK provides a generic http://docs.oracle.com/javase/8/docs/api/java/util/Iterator.html[`java.util.Iterator<T>`] interface and primitive versions of the Iterator in form of the sub-interfaces of http://docs.oracle.com/javase/8/docs/api/java/util/PrimitiveIterator.html[`java.util.PrimitiveIterator<T,T_CONS>`]. However, there are no primitive versions of the http://docs.oracle.com/javase/8/docs/api/java/lang/Iterable.html[`java.lang.Iterable<T>`] interface, constructing primitive iterators.

So the JDK is missing an interface to abstract over "a bunch" of primitive numbers to iterate over. A primitive iterator or primitive stream can only traversed once, which is not very satisfying in many cases. Ideally there should be in interface allowing the iteration over a (possibly infinite) sequence of primitive numbers. We want to be able to get a primitive iterator, a primitive stream, or directly iterate over the elements with a `forEach` method. A set of these interfaces is provided in package `de.fhg.fokus.xtensions.iteration`.<br>
The primitive Iterable versions provided in the package all specialize `java.lang.Iterable` with the boxed
number type, but also provide specialized functions for providing primitive iterators, primitive streams, and 
forEach methods that do not rely on boxing the primitive values when passing them on to the consumer.

In the following sections we will explore the ways to create those primitive Iterables.

Examples:

	import static extension de.fhg.fokus.xtensions.iteration.IntIterable.*
	// ...
	
	def printHex(IntIterable ints) {
		ints.forEachInt [
			val hex = Integer.toHexString(it)
			println(hex)
		]
	}
	
	def printHex(IntIterable ints, int limit) {
		val PrimitiveIterator.OfInt iter = ints.iterator
		for(var counter = 0; iter.hasNext && counter < limit; counter++) {
			val i = iter.nextInt
			val hex = Integer.toHexString(i)
			println(hex)
		}
	}
	
	def printHexOdd(IntIterable ints) {
		val IntStream s = ints.stream.filter[it % 2 == 1]
		s.forEach [
			val hex = Long.toHexString(it)
			println(hex)
		]
	}


==== From Iterables

Iterables can be mapped to primitive iterables by the special map extension functions `mapInt`, `mapLong`
and `mapDouble` defined in `de.fhg.fokus.xtensions.iteration.IterableExtensions`.

Example:

	import static extension de.fhg.fokus.xtensions.iteration.IterableExtensions.*
	// ...
	val lengths = newArrayList("foo", "baaaar", "bz").mapInt[length]


==== From Arrays

The `asIntIterable` extension method method creates a primitive iterable for primitive arrays.
There are two versions: One version creates an iterable over the complete array, the other one produces
an iterable over a section of the array. The section can be specified by defining the start index and
an excluding end index. 

Example:

	import static extension de.fhg.fokus.xtensions.iteration.PrimitiveArrayExtensions.*
	// ...
	val int[] arr = #[0,2,4,19,-10,10_000,Integer.MAX_VALUE,Integer.MIN_VALUE]
	var ints = arr.asIntIterable(1, arr.length - 1)  // omit first and last element


==== From Computations

Currently only available on IntIterable

////
TODO: Describe IntIterable.generate  
////

Example:

	import static extension de.fhg.fokus.xtensions.iteration.IntIterable.*
	// ...
	val ints = IntIterable.generate [
		val rand = new Random;
		[rand.nextInt]
	]

////
TODO: Describe IntIterable.iterate(int, IntUnaryOperator)  
////

Example:

	import static extension de.fhg.fokus.xtensions.iteration.IntIterable.*
	// ...
	val ints = IntIterable.iterate(1)[it * 2]


////
TODO: Describe IntIterable.iterate(int, IntPredicate, IntUnaryOperator)  
////

Example:

	import static extension de.fhg.fokus.xtensions.iteration.IntIterable.*
	// ...
	val ints = IntIterable.iterate(0, [it<=10], [it+2])


==== From Xtend Ranges

Creating iterables from `org.eclipse.xtext.xbase.lib.IntegerRange` can be done via the extensions 
class `de.fhg.fokus.xtensions.range.RangeExtensions`.

Example:

	import static org.eclipse.xtext.xbase.lib.IntegerRange.*
	// ...
	val iter = (0..50).withStep(2).asIntIterable


Creating iterables from `org.eclipse.xtext.xbase.lib.ExclusiveRange`s is currently not supported,
due to the API limitations on that class. 


==== From Primitive Optionals

The extension classes for primitive Optionals allow the creation of primitive iterables allowing 
iteration over either one or no value, depending on the source Optional.

Example:

	import static extension de.fhg.fokus.xtensions.optional.OptionalIntExtensions.*
	// ...
	val ints = some(42).asIterable


=== Extensions to String 

The class `de.fhg.fokus.xtensions.string.StringSplitExtensions` provides extension methods
for `java.lang.String` allowing to lazily split a string value.


The extension method `splitIt` returns an `Iterator` which lazily performs string split 
operations based on a regular expression (same `String#split(String)`) would do, but 
lazily. This allows the use of Iterator extension methods provided by Xtend and to stop splitting
a string when a condition is met without splitting the complete input string beforehand.

Example: 

	import static extension de.fhg.fokus.xtensions.string.StringSplitExtensions.*
	// ...
	val Iterator<String> i = "foozoobaar".splitIt("(?<=oo)")
	i.takeWhile[!startsWith("b")].forEach[
		println(it)
	]


If a split pattern is known in advance the following is possible with the JDK types to obtain a Stream of split elements:

	static extension val pattern = Pattern.compile("mypattern")
	// ...
	"tosplit".splitAsStream  // actually calls pattern.splitAsStream("tosplit")
	
If a pattern String has to be produced dynamically, the extension method `splitAsStream` is provided
as a shortcut for the sequence of calls from above:

	import static extension de.fhg.fokus.xtensions.string.StringSplitExtensions.*
	// ...
	val String patternStr = ... // dynamically created pattern
	"tosplit".splitAsStream(patternStr)


The class `de.fhg.fokus.xtensions.string.SptringMatchExtensions` provides extension methods to
`java.lang.String`, allowing to match regular expressions lazily via iterators.

To manually get matches for a pattern from an input string with JDK classes the following sequence has to be used:

	import java.util.regex.Pattern
	// ...
	val String input = "foo bar boo"
	val Pattern pattern = Pattern.compile("(\\woo)")
	val matcher = pattern.matcher(input)
	while(matcher.find) {
		val match = input.subSequence(matcher.start, matcher.end)
		// Do something with match
		println(match)
	}

The extension method `matchIt` elegantly wrapps this usage pattern into an Iterator, so the Xtend combinators
can be used on them.

	import static extension de.fhg.fokus.xtensions.string.StringMatchExtensions.*
	import java.util.regex.Pattern
	// ...
	val String input = "foo bar boo"
	val Pattern pattern = Pattern.compile("(\\woo)")
	input.matchIt(pattern).forEach [
		println(it)
	]

The method `matchIt` is overloaded to also take a string of the pattern, which internally compiles 
it to a pattern.

////
TODO: Describe extension method matchResultIt  
////


=== Extensions to Duration 

The class `de.fhg.fokus.xtensions.datetime.DurationExtensions` provides static extension
method for the JDK class `java.time.Duration`

////
TODO: Describe constructor extensions (e.g. long#seconds)  
TODO: Describe operators (+, -, /, *, >, <, >=, <=)
////


=== Extensions to Functions

////
TODO: Describe >>> operator, also for Pair (should be inlined). Usefull when   

Function Composition  
TODO: Describe andThen, >> etc.

TODO: Describe Bool functions and / or / negate

Throwing Functions  
TODO: Describe Function#filterException, Function#recoverException, etc.
////


=== Extensions to CompletableFuture

////
TODO: Describe then-Methods  
TODO: Describe whenCancelled/whenCancelledAsync extension method  
TODO: Describe whenException/whenExceptionAsync extension method  
TODO: Describe whenException/whenExceptionAsync extension method  
TODO: Describe recoverWith/recoverWithAsnyc extension method  
TODO: Describe handleCancellation/handleCancellationAsync extension method  
TODO: Describe forwardTo extension method  
TODO: Describe forwardCancellation extension method  
TODO: Describe cancelOnTimeout extension method  
TODO: Describe whenCancelledInterrupt method  

TODO: Describe Java 9 forward compatibility  
* TODO: Describe extension method orTimeout
* TODO: Describe extension method copy
////


=== Async Computations

////
TODO: Describe asyncRun methods  
TODO: Describe asyncSupply methods  
////


=== Scheduling Util

The class `de.fhg.fokus.xtensions.concurrent.SchedulingUtil` provides several static methods 
and static extension methods to easily schedule action for deferred or repeated execution.  
All operations have overloaded variants taking a `java.util.concurrent.ScheduledExecutorService`
as the first parameter, so these methods can be used as extension methods.

To repeat an action with a given period of time (starting immediately) you can use one of the overloaded 
versions of the `repeatEvery` method.

Example:

	import static extension de.fhg.fokus.xtensions.concurrent.SchedulingUtil.*
	import static extension de.fhg.fokus.xtensions.datetime.DurationExtensions.*
	// ...
	val hundredMs = 100.milliseconds
	repeatEvery(hundredMs) [
		println(currentTimeMillis)
	]

To repeat an action with a given period, starting with a delay instead of immediately, an overloaded
version of the `repeatEvery` method can be used:

Example:

	import static extension de.fhg.fokus.xtensions.concurrent.SchedulingUtil.*
	import java.util.concurrent.TimeUnit
	// ...
	repeatEvery(100, TimeUnit.MILLISECONDS).withInitialDelay(200) [
		println("Delayed start, repeated every 100 milis period")
	]

Note that the action will stop being repeatedly called if the action throws an exception or the future
returned by the `repeatEvery` method will be completed (e.g. by canceling it). This can either either 
be done by the action itself (the future will be passed to the action as parameter), or from the outside.  
Since the future is both passed to the action and returned, this also allows the action to check e.g. for
cancellation from the outside and aborting the action early.


	import static extension de.fhg.fokus.xtensions.concurrent.SchedulingUtil.*
	import static extension de.fhg.fokus.xtensions.datetime.DurationExtensions.*
	// ...
	val hundredMs = 100.milliseconds
	val fut = repeatEvery(hundredMs) [
		for(i : 0..Integer.MAX_VALUE) {
			if(cancelled) {
				println("I've been cancelled at iteration " + i)
				return
			}
		}
	]
	fut.cancel(false)


The method `delay` will defer the one-time execution of a given action by the given duration.
The delayed execution can be aborted before being started by completing the future returned by 
the `delay` method.  
The future returned by the `delay` method is also passed as a parameter to the deferred action.
If the future is completed before the delay is expired, the action will not be executed. If the 
action is performed, it can check during execution if the future is completed, e.g. to return 
prematurely (abort the action early).

TODO: Example

The method `waitFor` will create a `CompletableFuture` that will be completed successfully
with a `null` value when the given duration expires.

TODO: Example


== Build

The build is based on maven tycho, so http://maven.apache.org/download.cgi[Maven 3.0] or higher has to be installed on 
the machine.

To build the libraries from source, simply drop into the root directory and call `mvn clean package`.
The main library will be located in `bundles/de.fhg.fokus.xtensions/target`

// TODO describe where p2 is located


== Setting up the Development Environment

The repository contains the Oomph setup file link:releng/de.fhg.fokus.xtensions.releng/Xtensions.setup[Xtensions.setup] for the Eclipse IDE.


== Status of the Project

The following tasks are the next goals for this project, roughly in this order:

- [ ] Complete this README.adoc file
    - [ ] Complete Usage section
    - [ ] Complete IDE Setup section
- [ ] Complete JavaDocs for all public methods
- [ ] Cover each method with test cases
- [ ] Add Jacoco test coverage to maven build
- [ ] Add source bundle build to maven config
- [ ] Move to a public GitHub repository
- [ ] Create CI build on travis.io
- [ ] Publish Jacoco results to coveralls.io + add badge to this file
- [ ] Publish p2 repo on bintray
- [ ] Figure out how to best publish to Maven Central
- [ ] Release first version
- [ ] Add javadoc.io badge to this file
